META_START
    name: client
    parameters: server_id, alpha, beta, theta1, theta2, dummy0, dummy1
    csockets: 0 -> server
    epr_sockets: 0 -> server
META_END

^b0 {type = CL}:
    csocket = assign_cval() : 0
    const_1 = assign_cval() : 1

    epr0_rot_y = assign_cval() : 24
    epr0_rot_z = assign_cval() : 16
    epr0_rot_y = bcond_mult_const(epr0_rot_y, dummy0) : 0
    epr0_rot_z = bcond_mult_const(epr0_rot_z, dummy0) : 0
    theta2 = bcond_mult_const(theta2, dummy0) : 0

    epr1_rot_y = assign_cval() : 24
    epr1_rot_z = assign_cval() : 16
    epr1_rot_y = bcond_mult_const(epr1_rot_y, dummy1) : 0
    epr1_rot_z = bcond_mult_const(epr1_rot_z, dummy1) : 0
    theta1 = bcond_mult_const(theta1, dummy1) : 0

^b1 {type = QC}:
    run_request() : req
^b2 {type = QL}:
    tuple<p2> = run_subroutine(tuple<theta2; epr0_rot_y; epr0_rot_z>) : post_epr_0
^b4 {type = QL}:
    tuple<p1> = run_subroutine(tuple<theta1; epr1_rot_y; epr1_rot_z>) : post_epr_1

^b5 {type = CL}:
    // Here we are going to compute the angle to measure epr_1
    // p1 is our measurement outcome from measuring in the Z basis
    // So if p1 = 0 we are in |+_theta1> and p1 = 1 we are in |-_theta1> (I need to double check this again)
    // So here depending on p1, x = pi or x = 0
    // This is s_j * pij * pij * pij * pij * pij * pij * pij * pi 
    x = mult_const(p1) : 16
    
    minus_theta1 = mult_const(theta1) : -1
    // x - theta1
    delta1 = add_cval_c(minus_theta1, x)
    // x - theta1 + alpha
    // alpha is the actual angle we want to measure...
    // so if we do an X basis measurement (alpha = 0) we will teleport this state (|+>) to qubit 0
    delta1 = add_cval_c(delta1, alpha)

    // If this is not a dummy qubit, then minus_dummy0 also is 0
    minus_dummy0 = mult_const(dummy0) : -1

    // So if not a dummy qubit then this is 1
    should_correct_0 = add_cval_c(const_1, minus_dummy0)

    // So if not a dummy qubit we multiply alpha by 0
    delta1_correction = bcond_mult_const(alpha, should_correct_0) : 0

    // Then we multiply 0 by -1
    delta1_correction = mult_const(delta1_correction) : -1

    // then delta1 remains the same (if not a dummy)
    delta1 = add_cval_c(delta1, delta1_correction)

    send_cmsg(csocket, delta1)

^b6 {type = CC}:
    m1 = recv_cmsg(csocket)

^b7 {type = CL}:
    y = mult_const(p2) : 16
    minus_theta2 = mult_const(theta2) : -1
    beta = bcond_mult_const(beta, m1) : -1
    delta2 = add_cval_c(beta, minus_theta2)
    delta2 = add_cval_c(delta2, y)

    minus_dummy1 = mult_const(dummy1) : -1
    should_correct_1 = add_cval_c(const_1, minus_dummy1)
    delta2_correction = bcond_mult_const(beta, should_correct_1) : 0
    delta2_correction = mult_const(delta2_correction) : -1
    delta2 = add_cval_c(delta2, delta2_correction)

    send_cmsg(csocket, delta2)

^b8 {type = CC}:
    m2 = recv_cmsg(csocket)

^b9 {type = CL}:
    return_result(p1)
    return_result(p2)
    return_result(m1)
    return_result(m2)


SUBROUTINE post_epr_0
    params: theta2, epr0_rot_y, epr0_rot_z
    returns: p2
    uses: 0
    keeps:
    request:
  NETQASM_START
    // Need to prepare a randomized |+> state from a bell pair
    // theta2 is to hide the input
    // epr0_rot_y is 24 unless this is a dummy input (in which case 0)
    // epr1_rot_z is 16 unless this is a dummy input (in which case 0)
    // If this is a dummy input then the server is left with a qubit in |0> or |1>
    // This combo of RY RZ is equivalent to a Hadamard gate (up to some global phase)
    // After measuring we have teleported a |+> or |-> state to the server
    // However if theta2 is not 0, we first Z rotate by theta2
    // If after measuring in the Z basis we measure 0 we know the state is |+_theta2> 
    // If after measuring in the Z basis we measure 1 we know the state is |-_theta2>
    load C0 @input[0]
    load C1 @input[1]
    load C2 @input[2]
    set Q0 0
    rot_z Q0 C0 4
    rot_y Q0 C1 4
    rot_z Q0 C2 4
    meas Q0 M0
    store M0 @output[0]
  NETQASM_END

SUBROUTINE post_epr_1
    params: theta1, epr1_rot_y, epr1_rot_z
    returns: p1
    uses: 1
    keeps:
    request:
  NETQASM_START
    load C0 @input[0]
    load C1 @input[1]
    load C2 @input[2]
    set Q1 1
    rot_z Q1 C0 4
    rot_y Q1 C1 4
    rot_z Q1 C2 4
    meas Q1 M1
    store M1 @output[0]
  NETQASM_END

REQUEST req
  callback_type:
  callback: 
  return_vars: 
  remote_id: {server_id}
  epr_socket_id: 0
  num_pairs: 2
  virt_ids: increment 0
  timeout: 1000
  fidelity: 1.0
  typ: create_keep
  role: create

